apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

def properties = new Properties()
properties.load(new FileInputStream(rootProject.file("local.properties")))


android {
    compileSdk androidCompileSdkVersion

    defaultConfig {
        applicationId "com.eyedea.testmultimodule"
        minSdk androidMinSdkVersion
        targetSdk androidTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", 'POKEDEX_URL', properties.getProperty("POKEDEX_URL"))
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    def dependencyGroup = [
            core        : coreDependencies,
            main        : mainDependencies,
            ui          : uiDependencies,
            module      : moduleDependencies,
            notification: notificationDependencies,
            ciam        : ciamDependencies,
            localDb     : localDbDependencies
    ]

    def dependencyKaptGroup = [
            core   : coreKaptDependencies,
            localDb: localDbKaptDependencies
    ]

    def dependencyDebugGroup = [
            chuckerDebug: chuckerDebugDependencies
    ]

    def dependencyReleaseGroup = [
            chuckerRelease: chuckerReleaseDependencies
    ]

    dependencyGroup.each { key, dependencies ->
        dependencies.each { key2, dependency ->
            implementation dependency
        }
    }
    dependencyKaptGroup.each { key, dependencies ->
        dependencies.each { key2, dependency ->
            kapt dependency
        }
    }
    dependencyDebugGroup.each { key, dependencies ->
        dependencies.each { key2, dependency ->
            debugImplementation dependency
        }
    }
    dependencyReleaseGroup.each { key, dependencies ->
        dependencies.each { key2, dependency ->
            releaseImplementation dependency
        }
    }

    testImplementations.each { key, value ->
        if (key.startsWith('android')) {
            androidTestImplementation value
        } else {
            testImplementation value
        }
    }

    api project(":features:dashboard")
    api project(":features:core")
    api project(":features:util")
    api project(":features:info")


}