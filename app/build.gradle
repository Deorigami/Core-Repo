apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdk androidCompileSdkVersion

    defaultConfig {
        applicationId "com.byu.id"
        minSdk androidMinSdkVersion
        targetSdk androidTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", 'POKEDEX_URL', properties.getProperty("POKEDEX_URL"))
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    def dependencyGroup = [
            core        : coreDependencies,
            main        : mainDependencies,
            ui          : uiDependencies,
            module      : moduleDependencies,
            localDb     : localDbDependencies,
            firebase    : firebaseDependencies
    ]

    def dependencyKaptGroup = [
            core   : coreKaptDependencies,
            localDb: localDbKaptDependencies
    ]

    def dependencyDebugGroup = [
            chuckerDebug: chuckerDebugDependencies
    ]

    def dependencyReleaseGroup = [
            chuckerRelease: chuckerReleaseDependencies
    ]

    dependencyGroup.each { key, dependencies ->
        dependencies.each { key2, dependency ->
            if (key == "firebase" && key2 == "bom")
                implementation platform(dependency)
            else
                implementation dependency
        }
    }
    dependencyKaptGroup.each { key, dependencies ->
        dependencies.each { key2, dependency ->
            kapt dependency
        }
    }
    dependencyDebugGroup.each { key, dependencies ->
        dependencies.each { key2, dependency ->
            debugImplementation dependency
        }
    }
    dependencyReleaseGroup.each { key, dependencies ->
        dependencies.each { key2, dependency ->
            releaseImplementation dependency
        }
    }

    testImplementations.each { key, value ->
        if (key.startsWith('android')) {
            androidTestImplementation value
        } else {
            testImplementation value
        }
    }

    api project(':features:feature_dashboard')
    api project(":features:core")
    api project(':features:feature_util')
    api project(':features:feature_info')
    api project(':features:feature_onboarding')


}
